#!perl
use strict;
use warnings;

# usage: build-rfc-index rfc-index.xml
# ...probably retrieved from https://www.rfc-editor.org/in-notes/rfc-index.xml

use JSON;
use XML::LibXML;

open my $fh, '<', "$ARGV[0]"
  or die "error making handle to XML results: $!";

my $doc = XML::LibXML->load_xml(IO => $fh);

my %title;

my @rfcs = $doc->getElementsByTagName('rfc-entry');
for my $rfc (@rfcs) {
  my ($doc_id_elem) = $rfc->getElementsByTagName('doc-id');
  next unless $doc_id_elem;
  my $doc_id = $doc_id_elem->textContent;

  my ($title_elem)  = $rfc->getElementsByTagName('title');
  next unless $title_elem;
  my $title = $title_elem->textContent;

  my ($number) = $doc_id =~ /^RFC([0-9]+)\z/;
  next unless $number;

  $title{ 0+$number } = $title;
}

print JSON->new->canonical->pretty->encode(\%title);
